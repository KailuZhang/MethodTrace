apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: "com.gradle.plugin-publish"

sourceSets {
    main {
        groovy {
            srcDir '../methodtrace/src/main/groovy'
        }

        java {
            srcDir "../methodtrace/src/main/java"
        }

        resources {
            srcDir '../methodtrace/src/main/resources'
        }
    }
}

repositories {
    jcenter()
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.13.0"
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation group: 'org.ow2.asm', name: 'asm', version: '9.0'
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: '9.0'
    implementation 'com.android.tools.build:gradle:4.1.2'
    implementation 'com.android.tools.lint:lint:27.1.2'
    implementation 'com.android.tools.lint:lint-api:27.1.2'
    implementation 'com.android.tools.lint:lint-checks:27.1.2'
}

compileGroovy {
    dependsOn tasks.getByPath('compileKotlin')
    classpath += files(compileKotlin.destinationDir)
}

version = "1.0.11"    //配置插件的版本号
group = "io.github.kailuzhang"   //插件的群组，插件的classpath会用到

pluginBundle {

    website = 'https://github.com/kailuzhang/MethodTrace'
    vcsUrl = 'https://github.com/kailuzhang/MethodTrace.git'
    description = 'method time consuming trace(方法耗时插桩)'  //插件描述
    tags = ['方法耗时', 'method', 'trace', 'asm','Android']  //插件的tag。可以通过这个在插件门户上搜索

    plugins {
        greetingsPlugin {
            id = 'io.github.kailuzhang.methodtrace'   //插件的id，到时候apply的插件id
            displayName = 'MethodTrace Plugin'
        }
    }
}

apply from: rootProject.file('gradle/java-publish.gradle')